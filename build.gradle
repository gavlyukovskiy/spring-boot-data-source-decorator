buildscript {
    repositories {
        jcenter()
    }
}

plugins {
    id "com.jfrog.bintray" version "1.7.3"
    id "pl.allegro.tech.build.axion-release" version "1.7.1"
}

def pomConfig = {
    licenses {
        license {
            name "The Apache Software License, Version 2.0"
            url "http://www.apache.org/licenses/LICENSE-2.0.txt"
            distribution "repo"
        }
    }
    developers {
        developer {
            id "gavlyukovskiy"
            name "Arthur Gavlyukovskiy"
            email "agavlyukovskiy@gmail.com"
        }
    }

    scm {
        url "https://github.com/gavlyukovskiy/spring-boot-data-source-decorator"
    }
}

subprojects {
    ext {
        springBootVersion = '1.4.2.RELEASE'
        p6SpyVersion = '3.2.0'
        datasourceProxyVersion = '1.4.2'
        flexyPoolVersion = '1.2.9'

        release = !project.name.contains("sample")
    }

    apply plugin: 'java'

    scmVersion {
        versionIncrementer 'incrementPatch'
        tag {
            prefix = ''
            versionSeparator = ''
        }

        nextVersion {
            suffix = 'rc'
        }
    }

    group 'com.github.gavlyukovskiy'
    version = scmVersion.version

    sourceCompatibility = 1.8

    repositories {
        mavenCentral()
    }

    if (ext.release) {
        apply plugin: 'maven-publish'
        apply plugin: 'com.jfrog.bintray'

        publishing {
            publications {
                mavenJava(MavenPublication) {
                    from components.java
                    artifact sourceJar
                    artifact javadocJar
                    groupId project.group
                    artifactId project.name
                    version project.version
                    pom.withXml {
                        def root = asNode()
                        root.appendNode('description', 'Spring Boot integration with p6spy, datasource-proxy and flexy-pool')
                        root.appendNode('name', 'spring-boot-data-source-decorator')
                        root.appendNode('url', 'https://github.com/gavlyukovskiy/spring-boot-data-source-decorator')
                        root.children().last() + pomConfig
                    }
                }
            }
        }

        bintray {
            user = System.getenv('BINTRAY_USER')
            key = System.getenv('BINTRAY_KEY')
            publications = ['mavenJava']
            pkg {
                repo = project.group
                name = project.name
                licenses = ['Apache-2.0']
                version {
                    name = project.version
                    released = new Date()
                    vcsTag = scmVersion.tag.prefix + scmVersion.tag.versionSeparator + scmVersion.version
                }
            }
        }

        task sourceJar(type: Jar, dependsOn: classes) {
            classifier 'sources'
            from sourceSets.main.allSource
        }

        task javadocJar(type: Jar, dependsOn: javadoc) {
            classifier = 'javadoc'
            from javadoc.destinationDir
        }

        tasks.withType(Jar) {
            from(rootProject.projectDir) {
                include 'LICENSE.txt'
                into 'META-INF'
            }
        }
    }
}
